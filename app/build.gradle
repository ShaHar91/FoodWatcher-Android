apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

// define build version
def versionMajor = 1
def versionMinor = 0
def versionPatch = 2
def versionBuild = 0

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.shahar91.foodwatcher"
        minSdkVersion 23
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // generate versionCode and versionName from defined major, minor, versionPatch, versionBuild
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

        setProperty("archivesBaseName", applicationId + "-v" + versionCode + "(" + versionName + ")")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    flavorDimensions 'env'
    productFlavors {
        dev {
            dimension 'env'
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
        stag {
            dimension 'env'
            applicationIdSuffix '.stag'
            versionNameSuffix '-stag'
        }
        prod {
            dimension 'env'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':core')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    kapt "com.android.databinding:compiler:4.0.0"

    kapt "androidx.room:room-compiler:2.2.6"

    implementation 'com.github.kizitonwose:CalendarView:1.0.2'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    // https://github.com/reddit/IndicatorFastScroll
    implementation 'com.reddit:indicator-fast-scroll:1.4.0'
}